plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'net.zithium'
version = '2.7.8'
description = 'DeluxeCoinflip'

repositories {
    mavenCentral()
    maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
    maven { url = uri('https://ci.ender.zone/plugin/repository/everything/') }
    maven { url = uri('https://raw.githubusercontent.com/TeamVK/maven-repository/master/release/') }
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://repo.codemc.org/repository/maven-public/') }
    maven { url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/') }
    maven { url = uri('https://libraries.minecraft.net/') }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
}

dependencies {
    implementation 'me.mattstudios.utils:matt-framework:1.4.6'
    implementation 'dev.triumphteam:triumph-gui:3.1.7'
    implementation 'org.bstats:bstats-bukkit:3.0.2'

    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'

    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'com.mojang:authlib:1.5.21'

    compileOnly('com.github.Realizedd:TokenManager:3.2.4') { transitive = false; }
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'com.vk2gpz.tokenenchant:TokenEnchantAPI:18.37.1'
    compileOnly 'org.black_ixx:playerpoints:3.2.6'
    compileOnly fileTree(dir: 'libs', include: '*.jar')
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava {
    options.encoding = "UTF-8"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


shadowJar {
    minimize()
    archiveFileName = "DeluxeCoinflip-${project.version}.jar"
    relocate 'dev.triumphteam.gui', 'net.zithium.deluxecoinflip.libs.gui'
    relocate 'me.mattstudios.mf', 'net.zithium.deluxecoinflip.libs.command'
    relocate 'org.bstats', 'net.zithium.deluxecoinflip.libs.metrics' // bStats
}