plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
}

group = 'net.zithium'
version = '2.8.2'
description = 'DeluxeCoinflip'

repositories {
    mavenCentral()
    maven { url = uri('https://repo.papermc.io/repository/maven-public/') }
    maven { url = uri('https://ci.ender.zone/plugin/repository/everything/') }
    maven { url = uri('https://raw.githubusercontent.com/TeamVK/maven-repository/master/release/') }
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://repo.codemc.org/repository/maven-public/') }
    maven { url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/') }
    maven { url = uri('https://libraries.minecraft.net/') }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
}

dependencies {
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation 'dev.triumphteam:triumph-gui:3.1.11'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'com.github.ItzSave:ZithiumLibrary:v2.1.0'

    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'

    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'org.jetbrains:annotations:24.1.0'

    compileOnly('com.github.Realizedd:TokenManager:3.2.4') { transitive = false }
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'org.black_ixx:playerpoints:3.2.6'
    compileOnly fileTree(dir: 'libs', include: '*.jar')
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava {
    options.encoding = "UTF-8"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


shadowJar {
    minimize()
    archiveFileName = "DeluxeCoinflip-${project.version}.jar"
    relocate 'dev.triumphteam.gui', 'net.zithium.deluxecoinflip.libs.gui'
    relocate 'net.zithium.library', 'net.zithium.deluxecoinflip.libs.library'
    relocate 'org.bstats', 'net.zithium.deluxecoinflip.libs.metrics' // bStats
}